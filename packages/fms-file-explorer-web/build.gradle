buildscript {
    repositories {
        mavenLocal()
        maven {
            url "https://artifactory.corp.alleninstitute.org/artifactory/maven-virtual"
        }
        mavenCentral()
    }
    dependencies {
        classpath("org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1")
        classpath("org.alleninstitute.aics:gradle-plugins:2.0.11")
    }
}

plugins {
    id "com.dorongold.task-tree" version "1.3.1"
    id "com.moowork.node" version "1.2.0"
}

apply plugin: "maven-publish"
apply plugin: "maven-publish-auth"
apply plugin: "org.alleninstitute.git-info"

node {
    version = "10.16.3"
    npmVersion = "6.11.3"
    download = true
    nodeModulesDir = file("${project.projectDir}")
}

def packageJson = new File("package.json")
def parsed = new groovy.json.JsonSlurper().parseText(packageJson.text)
project.version = parsed.version
project.group = "org.alleninstitute.aics"

ext {
    // TGZ Publishing
    distParentDir = file("$projectDir/dist")
    artifactVersion = gitInfo.branch != "master" ? "${gitInfo.branch.tokenize("/").join("-")}" : project.version
    artifactName = "${project.name}-${ext.artifactVersion}.tgz"
}

wrapper {
    gradleVersion "5.6.2"
    distributionType Wrapper.DistributionType.BIN
}

///////////////////////////
// Build and publish
//////////////////////////

task bundle(type: NpmTask) {
    args = "run bundle".tokenize()
}

/// Publishing
task packageForPublish(type: Tar) {
    dependsOn bundle

    from distParentDir
    destinationDir file("${projectDir}")
    archiveName "$artifactName"
    compression Compression.GZIP
}

task artifactClean(type: Delete) {
    delete "$artifactName"
}

task snapshotPublish {
    description "Create production module and publish to snapshot repo in artifactory"
    group "AICS Publishing"
    dependsOn packageForPublish, "publishMvnModulePublicationToSnapshotsRepoRepository"
}

publishing {
    publications {
        mvnModule(MavenPublication) {
            version artifactVersion
            artifact packageForPublish
        }
    }

    repositories {
        maven {
            name = "snapshotsRepo"
            url "https://artifactory.corp.alleninstitute.org/artifactory/maven-snapshot-local"
        }
    }
}
